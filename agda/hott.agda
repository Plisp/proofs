{-# OPTIONS --without-K --exact-split --safe #-}

{-
  univalent math, hott chapter 3
-}

open import Agda.Primitive
open import logic
open import types
open import paths

{-
  -1-type (contractible)
-}

is-center : (X : Set â„“) â†’ X â†’ Set â„“
is-center X c = (x : X) â†’ c ï¼ x

is-contr : Set â„“ â†’ Set â„“
is-contr X = Î£ c âˆ¶ X , is-center X c

ğŸ™-is-singleton : is-contr ğŸ™
ğŸ™-is-singleton = â‹† , indâŠ¤ (Î» x â†’ â‹† ï¼ x) (refl â‹†)

center : (X : Set â„“) â†’ is-contr X â†’ X
center X (c , p) = c

centrality : (X : Set â„“) (i : is-contr X) â†’ (x : X) â†’ center X i ï¼ x
centrality X (c , p) = p

singleton-type : {X : Set â„“} â†’ X â†’ Set â„“
singleton-type {â„“} {X} x = Î£ y âˆ¶ X , y ï¼ x

singleton-type-center : {X : Set â„“} (x : X) â†’ singleton-type x
singleton-type-center x = (x , refl x)

-- refl makes this trivial, since we have any (x, x ï¼ y) is simply (x, refl x)
singleton-type-centered : {X : Set â„“} (x : X) (Ïƒ : singleton-type x)
                        â†’ singleton-type-center x ï¼ Ïƒ
singleton-type-centered x (x , refl x) = refl (x , refl x)

 -- is-contr (Î£ y , y ï¼ x) is of the form Î£ c , (x , refl x) ï¼ c
singleton-types-are-singletons : (X : Set â„“) (x : X)
                               â†’ is-contr (singleton-type x)
singleton-types-are-singletons X x
  = singleton-type-center x , singleton-type-centered x

-- (subtype) singletons but maybe not inhabited
is-subsingleton : Set â„“ â†’ Set â„“
is-subsingleton X = (x y : X) â†’ x ï¼ y

ğŸ˜-is-subsingleton : is-subsingleton ğŸ˜
ğŸ˜-is-subsingleton x y = indâŠ¥ (Î» x â†’ (x ï¼ y)) x

is-prop = is-subsingleton

singletonâ†’subsingleton : (X : Set â„“) â†’ is-contr X â†’ is-subsingleton X
singletonâ†’subsingleton X (c , p) x y = symï¼ (p x) âˆ™ p y

pointed-subsingletonâ†’singleton : (X : Set â„“) â†’ X
                               â†’ is-subsingleton X â†’ is-contr X
pointed-subsingletonâ†’singleton X x s = (x , s x)

{-
  n-types/groupoids â†” hlevel n+2
-}

0-type : Set â„“ â†’ Set â„“
0-type X = (x y : X) â†’ is-subsingleton (x ï¼ y)

is-set = 0-type

1-type : Set â„“ â†’ Set â„“
1-type X = {x y : X} (p q : x ï¼ y) â†’ is-subsingleton (p ï¼ q)

_is-of-hlevel_ : Set â„“ â†’ â„• â†’ Set â„“
X is-of-hlevel zero    = is-contr X
X is-of-hlevel (suc n) = (x x' : X) â†’ ((x ï¼ x') is-of-hlevel n)

subsingletonâ†’set : (X : Set â„“) â†’ is-subsingleton X â†’ is-set X
subsingletonâ†’set X ss = proof
  where
    g : {x : X} (y : X) â†’ x ï¼ y
    g {x} y = ss x y

    lemma : {x y y' : X} (r : y ï¼ y') â†’ (g y) âˆ™ r ï¼ g y'
    lemma {x}{y} r = symï¼ (transportpqï¼qâˆ™p r (g y)) âˆ™ apd (g {x}) r

    proof : (x y : X) (p q : x ï¼ y) â†’ p ï¼ q
    proof x y p q = lcancelâˆ™ (g {x} x) p q (lemma p âˆ™ symï¼ (lemma q))

-- the levels are upper closed
hlevel-suc : (X : Set â„“) (n : â„•)
           â†’ X is-of-hlevel n â†’ X is-of-hlevel (suc n)
hlevel-suc X zero    = Î» h â†’ Î» x y â†’
                         let k = singletonâ†’subsingleton X h in
                             (k x y , subsingletonâ†’set X k x y (k x y))
-- lift H (suc n) X using X = (xï¼y) in ind (H n T -> H (suc n) T)
hlevel-suc X (suc n) = Î» h â†’ Î» x y â†’ hlevel-suc (x ï¼ y) n (h x y)

-- equalities are of a hlevel that's one less
hlevel-eq : {X : Set â„“} {n : â„•}
          â†’ X is-of-hlevel (suc n)
          â†’ (x y : X) â†’ (x ï¼ y) is-of-hlevel n
hlevel-eq {â„“}{X} {n} p x y = p x y

ğŸ˜-is-set : is-set ğŸ˜
ğŸ˜-is-set = subsingletonâ†’set ğŸ˜ ğŸ˜-is-subsingleton

{-
  decidable
-}

decidable : Set â„“ â†’ Set â„“
decidable A = A ï¼‹ Â¬ A

has-decidable-equality : Set â„“ â†’ Set â„“
has-decidable-equality A = (x y : A) â†’ decidable (x ï¼ y)

is-empty : Set â„“ â†’ Set â„“
is-empty X = Â¬ X

LEM LEM' : âˆ€ â„“ â†’ Set (lsuc â„“)
LEM â„“ = (X : Set â„“) â†’ is-prop X â†’ decidable X
LEM' â„“ = (X : Set â„“) â†’ is-subsingleton X â†’ is-contr X ï¼‹ is-empty X

{-
  retracts
-}

-- right inverse
has-section : {X : Set â„“} {Y : Set â„“â‚} â†’ (X â†’ Y) â†’ Set (â„“ âŠ” â„“â‚)
has-section {â„“}{â„“â‚} {X}{Y} f = Î£ g âˆ¶ (Y â†’ X) , f âˆ˜ g ~ id

-- X type is a retract of Y
_â—_ : Set â„“ â†’ Set â„“â‚ â†’ Set (â„“ âŠ” â„“â‚)
X â— Y = Î£ f âˆ¶ (Y â†’ X) , has-section f

retraction : {X : Set â„“} {Y : Set â„“â‚} â†’ X â— Y â†’ (Y â†’ X)
retraction (r , s , Î·) = r

section : {X : Set â„“} {Y : Set â„“â‚} â†’ X â— Y â†’ (X â†’ Y)
section (r , s , Î·) = s

is-retract : {X : Set â„“} {Y : Set â„“â‚} â†’ (p : X â— Y)
           â†’ retraction p âˆ˜ section p ~ id
is-retract (r , s , Î·) = Î·

reflâ— : (X : Set â„“) â†’ X â— X
reflâ— X = id , id , refl

_â—âˆ™_ : {X : Set â„“} {Y : Set â„“â‚} {Z : Set â„“â‚‚} â†’ X â— Y â†’ Y â— Z â†’ X â— Z
(r , s , Î·) â—âˆ™ (r' , s' , Î·') = r âˆ˜ r' , s' âˆ˜ s , Î» x â†’ ap r (Î·' (s x)) âˆ™ Î· x

_â—âŸ¨_âŸ©_ : (X : Set â„“) {Y : Set â„“â‚} {Z : Set â„“â‚‚} â†’ X â— Y â†’ Y â— Z â†’ X â— Z
X â—âŸ¨ xâ—y âŸ© yâ—z = xâ—y â—âˆ™ yâ—z

-- a natural transformation between two fibrations
Nat : {X : Set â„“} â†’ (X â†’ Set â„“â‚) â†’ (X â†’ Set â„“â‚‚) â†’ Set (â„“ âŠ” â„“â‚ âŠ” â„“â‚‚)
Nat A B = âˆ€ x â†’ A x â†’ B x
-- gives a map between their total spaces
NatÎ£ : {X : Set â„“} {A : X â†’ Set â„“â‚} {B : X â†’ Set â„“â‚‚} â†’ Nat A B â†’ Î£ A â†’ Î£ B
NatÎ£ Ï„ (x , a) = (x , Ï„ x a)

Î£-retract : {X : Set â„“} {A : X â†’ Set â„“â‚} {B : X â†’ Set â„“â‚‚}
          â†’ ((x : X) â†’ A x â— B x) â†’ Î£ A â— Î£ B
Î£-retract {â„“}{â„“â‚}{â„“â‚‚} {X} {A} {B} Ï = NatÎ£ r , NatÎ£ s , Î·
  where
    r = Î» x â†’ retraction (Ï x)
    s = Î» x â†’ section (Ï x)
    Î·' : (x : X) â†’ r x âˆ˜ s x ~ id
    Î·' x = is-retract (Ï x)

    Î· : NatÎ£ r âˆ˜ NatÎ£ s ~ id
    Î· (x , a) = ap (Î» - â†’ (x , -)) (Î·' x a)

transport-is-retraction : {X : Set â„“} (A : X â†’ Set â„“â‚) {x y : X} (p : x ï¼ y)
                        â†’ transport A p âˆ˜ transport A (symï¼ p) ~ id
transport-is-retraction A p = id~ (transport-p-sym p)

transport-is-section : {X : Set â„“} (A : X â†’ Set â„“â‚) {x y : X} (p : x ï¼ y)
                     â†’ transport A (symï¼ p) âˆ˜ transport A p ~ id
transport-is-section A p = id~ (transport-sym-p p)

-- retraction similar to above but only at basepoints X â— Y =A Y=
Î£-reindex-retract : {X : Set â„“} {Y : Set â„“â‚} {A : X â†’ Set â„“â‚‚}
                  â†’ (r : Y â†’ X) â†’ has-section r
                  â†’ (Î£ x âˆ¶ X , A x) â— (Î£ y âˆ¶ Y , A (r y))
Î£-reindex-retract {â„“}{â„“â‚}{â„“â‚‚} {X} {Y} {A} r (s , Î·) = ar , as , aÎ·
  where
   ar : Î£ (A âˆ˜ r) â†’ Î£ A
   ar (y , a) = (r y , a)

   as : Î£ A â†’ Î£ (A âˆ˜ r) -- A (id x) -> A (r (s x))
   as (x , a) = (s x , transport A (symï¼ (Î· x)) a)

   -- to-Î£ does a transport along the first equality which is cancelled
   aÎ· : ((x , a) : Î£ A) â†’ (r (s x) , transport A (symï¼ (Î· x)) a) ï¼ (x , a)
   aÎ· (x , a) = to-Î£ï¼ (Î· x , transport-is-retraction A (Î· x) a)

retract-of-singleton : {X : Set â„“} {Y : Set â„“â‚}
                     â†’ Y â— X â†’ is-contr X â†’ is-contr Y
retract-of-singleton (f , g , p) contr = f (center _ contr) , centr
  where
    centr : âˆ€ y â†’ f (center _ contr) ï¼ y
    centr y = ap f (centrality _ contr (g y)) âˆ™ (p y)

{-
  Voevodsky's equivalence
-}

-- space: witnesses x Ã— f x = y
fiber : {X :  Set â„“} {Y : Set â„“â‚} (f : X â†’ Y) â†’ Y â†’ Set (â„“ âŠ” â„“â‚)
fiber {â„“}{â„“â‚} {X}{Y} f y = Î£ x âˆ¶ X , f x ï¼ y

fiber-base : {X : Set â„“} {Y : Set â„“â‚} {f : X â†’ Y} {y : Y}
           â†’ fiber f y â†’ X
fiber-base (x , p) = x

fiber-id : {X : Set â„“} {Y : Set â„“â‚} {f : X â†’ Y} {y : Y}
         â†’ (w : fiber f y) â†’ f (fiber-base w) ï¼ y
fiber-id (x , p) = p

-- the fibre (preimage) of all y : Y under f is unique (size 1)
-- the proof is also unique, via the characterisation of Î£ identity
is-equiv : {X : Set â„“} {Y : Set â„“â‚} â†’ (X â†’ Y) â†’ Set (â„“ âŠ” â„“â‚)
is-equiv {â„“}{â„“â‚} {X}{Y} f = Î  y âˆ¶ Y , is-contr (fiber f y)

_â‰ƒ_ : Set â„“â‚ â†’ Set â„“â‚‚ â†’ Set (â„“â‚ âŠ” â„“â‚‚)
X â‰ƒ Y = Î£ f âˆ¶ (X â†’ Y) , is-equiv f
infix 5 _â‰ƒ_

equiv-fn : {X : Set â„“} {Y : Set â„“â‚} â†’ X â‰ƒ Y â†’ X â†’ Y
equiv-fn (f , i) = f

id-is-equiv : (X : Set â„“) â†’ is-equiv id
id-is-equiv = singleton-types-are-singletons

reflâ‰ƒ : (X : Set â„“) â†’ X â‰ƒ X
reflâ‰ƒ X = id , id-is-equiv X

-- inverses - center is p , Î£ x, f x ï¼ y
inverse : {X : Set â„“} {Y : Set â„“â‚} (f : X â†’ Y) â†’ is-equiv f â†’ (Y â†’ X)
inverse f equiv y = fiber-base (center (fiber f y) (equiv y))

{-
  relationship with invertibles
-}

invertible : {A : Set â„“} {B : Set â„“â‚} (f : A â†’ B) â†’ Set (â„“ âŠ” â„“â‚)
invertible {â„“}{â„“â‚} {A}{B} f = Î£ g âˆ¶ (B â†’ A) , g âˆ˜ f ~ id Ã— f âˆ˜ g ~ id

-- the easy direction
inverses-are-sections : {X : Set â„“} {Y : Set â„“â‚} (f : X â†’ Y) (e : is-equiv f)
                      â†’ f âˆ˜ inverse f e ~ id
inverses-are-sections f e y = fiber-id (center (fiber f y) (e y))

inverse-centrality : {X : Set â„“} {Y : Set â„“â‚}
                     (f : X â†’ Y) (e : is-equiv f) (y : Y) (t : fiber f y)
                   â†’ (inverse f e y , inverses-are-sections f e y) ï¼ t
inverse-centrality f e y = centrality (fiber f y) (e y)

inverses-are-retractions : {X : Set â„“} {Y : Set â„“â‚} (f : X â†’ Y) (e : is-equiv f)
                         â†’ inverse f e âˆ˜ f ~ id
inverses-are-retractions f e x = ap prâ‚ r
  where
    q : âˆ€ fb â†’ (center _ (e (f x))) ï¼ fb
    q = centrality _ (e (f x))
    -- inverse is just the base of the single fiber
    r : center (fiber f (f x)) (e (f x)) ï¼ (x , refl (f x))
    r = q (x , refl (f x))

equivs-are-invertible : {X : Set â„“} {Y : Set â„“â‚} (f : X â†’ Y)
                      â†’ is-equiv f â†’ invertible f
equivs-are-invertible f e = inverse f e ,
                            inverses-are-retractions f e ,
                            inverses-are-sections f e

-- the hard direction
invertibles-are-equivs : {X : Set â„“} {Y : Set â„“â‚} (f : X â†’ Y)
                       â†’ invertible f â†’ is-equiv f
invertibles-are-equivs f (g , gf , fg) = p
  where
    p1 : âˆ€ y â†’ ((x , _) : (fiber f y)) â†’ g y ï¼ x
    p1 y (x , fxï¼y) = symï¼ (ap g fxï¼y) âˆ™ gf x

    p2 : âˆ€ y â†’ ((x , fxï¼y) : fiber f y) --                 f(g y) = y
       â†’ transport (Î» e â†’ f e ï¼ y) (symï¼ (ap g fxï¼y) âˆ™ gf x) (fg y) ï¼ fxï¼y
    p2 y (x , refl y) = transport (Î» e' â†’ transport _ e' _ ï¼ _) q2 q3
      where -- f(g(f x)) ï¼ (f x)                    x ï¼ g(f(x))    f (x!) = f x
        q0 : fg (f x) ï¼ transport (Î» e â†’ f e ï¼ f x) (symï¼ (gf x)) (refl (f x))
        q0 = refl (fg (f x))

        q2 : gf x ï¼ symï¼ (ap g (refl y)) âˆ™ gf x
        q2 = begin (gf x)
               =âŸ¨ symï¼ (sym-volution (gf x)) âŸ©
                   symï¼ (symï¼ (gf x))
               =âŸ¨ symï¼ (ap symï¼ (pâˆ™reflï¼p (symï¼ (gf x)))) âŸ©
                   symï¼ (symï¼ (gf x) âˆ™ refl (g y))
               =âŸ¨ sym-homo-âˆ™ (symï¼ (gf x)) (refl (g (f x))) âŸ©
                   symï¼ (refl (g y)) âˆ™ symï¼ (symï¼ (gf x))
               =âŸ¨ ap (_âˆ™_ (refl (g (f x)))) (sym-volution (gf x)) âŸ©
                   symï¼ (refl (g y)) âˆ™ gf x
               =âŸ¨âŸ© symï¼ (ap g (refl y)) âˆ™ gf x âˆ

        q3 : transport (Î» e â†’ f e ï¼ f x) (gf x) (fg (f x)) ï¼ refl (f x)
        q3 = ap (Î» e â†’ transport (Î» e â†’ f e ï¼ f x) (gf x) e) q0
           âˆ™ transport-is-retraction _ (gf x) (refl (f x))

    p3 : âˆ€ y â†’ âˆ€ (fb : (fiber f y)) â†’ (g y , fg y) ï¼ fb
    p3 y fb@(x , fxï¼y) = to-Î£ï¼ (p1 y fb , p2 y fb)

    p : âˆ€ y â†’ Î£ c âˆ¶ (fiber f y) , is-center (fiber f y) c
    p y = (g y , fg y) , p3 y


-- corollaries
-- inverses-are-equivs : {X : Set â„“} {Y : Set â„“â‚} (f : X â†’ Y) (e : is-equiv f)
--                     â†’ is-equiv (inverse f e)
-- inverses-are-equivs = {!!}

{-
  univalence
-}

quasi-equiv : (A : Set â„“) (B : Set â„“â‚) â†’ Set (â„“ âŠ” â„“â‚)
quasi-equiv A B = Î£ f âˆ¶ (A â†’ B) , invertible f

Idâ†’Eq : (X Y : Set â„“) â†’ X ï¼ Y â†’ X â‰ƒ Y
Idâ†’Eq X X (refl X) = reflâ‰ƒ X

is-univalent : (â„“ : Level) â†’ Set (lsuc â„“)
is-univalent â„“ = âˆ€ (X Y : Set â„“) â†’ is-equiv (Idâ†’Eq X Y)

univalence-â‰ƒ : is-univalent â„“ â†’ (X Y : Set â„“) â†’ (X ï¼ Y) â‰ƒ (X â‰ƒ Y)
univalence-â‰ƒ ua X Y = Idâ†’Eq X Y , ua X Y

Eqâ†’Id : is-univalent â„“ â†’ (X Y : Set â„“) â†’ X â‰ƒ Y â†’ X ï¼ Y
Eqâ†’Id ua X Y = inverse (Idâ†’Eq X Y) (ua X Y)

Idâ†’fun : {X Y : Set â„“} â†’ X ï¼ Y â†’ X â†’ Y
Idâ†’fun {â„“} {X} {Y} p = equiv-fn (Idâ†’Eq X Y p)
